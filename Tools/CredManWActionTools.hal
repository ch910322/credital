external updating procedure DoBuyOutAgreement(record CredManVc,var record IVVc);
external inner procedure CredManUpdate(var record CredManVc,Integer,string);
external inner procedure CredManSumup(var record CredManVc);
external inner procedure CredManVc_PasteCurncyCode(var record CredManVc);
external procedure BuildCredManPayPlanCust(record CredManVc,var record CredManPayPlanVc,record CredManSetBlock,Boolean,Boolean);
external procedure CheckBuyOutAgreement(record CredManVc,var record RcVc);
external inner function Boolean PasteCustInCredMan(var record CredManVc);
external inner procedure CredManVc_PasteType(var record CredManVc);
external inner function Integer CreateCreditNoteIV(record IVVc,Integer,var record IVVc,string,Boolean);
external updating procedure PrintMultiDocDocument(string,string,string,Integer,Boolean,string);
external updating procedure PrintMultiDocDocument2(string,string,string,Integer,Boolean,string,record RcVc);
external inner function string 60 AddObjectToObjectList(string,string);
external inner procedure VIVc_PasteInvDate(var record VIVc);
external inner procedure VIDDefault(Integer,string,var record VIVc);
external inner function Boolean VIVc_PasteAccNumber(var record VIVc,string,Boolean,Integer);
external inner function Boolean VIVc_PasteVECode(var record VIVc,Integer,Boolean,Boolean,var string);
external inner procedure VIVc_PastePayDeal(var record VIVc);
external inner procedure VICalcVals(var record VIVc);
external inner procedure VISumup(record VIVc,var val);
external updating procedure CreateSingleAgreementInvoice(record CredManVc,Date,var record IVVc);
external inner procedure IVVc_PasteInvDate(var record IVVc,record LocalMachineBlock,var Integer,var Integer);
external updating procedure ReceiptEntryCreateReceipts(record SMVc,Boolean,var val);
external updating procedure CreateCredHist(Longint,Integer,string,val,Integer,string,Date,Longint,Date,var Longint,Date,Date,Integer,Longint,val,Longint,val);
external updating procedure UpdateCredHistBalancePartialBuyout(Longint,Longint);
external procedure PrintEBSDocument(string,string,record RcVc,string,string);
external inner procedure OPPastePayMode(var record OPVc);
external inner function Boolean PasteInvIn2OPr(var record OPVc,Integer,Date,Integer,val,var string,Boolean,var Boolean);
external inner function Boolean PasteInvIn2IPr(var record IPVc,Integer,Date,var val,Boolean,var Boolean);
external inner procedure IPPastePayMode(var record IPVc);
external inner procedure OPVc_PasteRecVal(var record OPVc,Integer);
external updating function Boolean ArchiveExistingDocument(record AgreementDocVc);
external updating procedure CheckAndCreateAutoMailFromInvoice(record IVVc);

global
procedure PasteCustInAgreementRemote(var record CredManVc CredManr)
begin
  record BankVc Bankr;
  record CUVc CUr;
  record BaseCurBlock BCb;


  CUr.Code = CredManr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    CredManr.BankCode = CUr.AccOperator;
    Bankr.Code = CUr.AccOperator;
    if (ReadFirstMain(Bankr,1,true)) then begin
      CredManr.BankName = Bankr.Name;
    end;
    CredManr.BankAccount = CUr.BankAccount;
    if (nonblank(CUr.CurncyCode)) then begin
      CredManr.CurncyCode = CUr.CurncyCode;
    end else begin
      BlockLoad(BCb);
      CredManr.CurncyCode = BCb.BaseCur1;
    end;
    CredManVc_PasteCurncyCode(CredManr);
  end;
  
  return;
end;

global
updating procedure TerminateCredMan(var record CredManVc CredManr,var record IVVc IVr)
begin
  record CMOrderClassBlock OCb;
  record CredManVc oldCredManr;
   
  BlockLoad(OCb);

  if (nonblank(OCb.TerminatedOrderClass)) then begin
    DoBuyOutAgreement(CredManr,IVr);
    RecordCopy(oldCredManr,CredManr);
    CredManr.OrderClass = OCb.TerminatedOrderClass;
    RecordUpdate(oldCredManr,CredManr,true);
  end;

  return;
end;

global
procedure CredManSetDatesCust(var record CredManVc CredManr)
begin
  record CredManSetBlock CredManSetb;
  Date startd,finv,fint,tsd;
  Integer invday;
  
  invday = CredManr.MonthlyPaymentDay;
  if (invday<1) then begin
    BlockLoad(CredManSetb);
    invday = CredManSetb.InvDay;
  end;
  startd = CredManr.startDate;
  //finv = AddMonth(startd,1); //CUST-CHANGED
  finv = AddDay(startd,25);//29
  tsd = finv;
  finv = AddDay(finv,invday - GetDay(finv));//-14
  if (tsd>finv) then begin
    finv = AddMonth(finv,1);
  end;
  CredManr.FirstInvDate = finv;
  CredManr.FirstIntDate = CredManr.startDate;
  CredManr.MoneyTransferDate = CredManr.startDate;
  CredManUpdate(CredManr,-1,"");
  CredManSumup(CredManr);
  CredManr.endDate = AddMonth(CredManr.FirstInvDate,CredManr.InstalmentMonths - 1);
/*
  if (nonblank(CredManr.PauseDate)) then begin
    CredManr.endDate = AddMonth(CredManr.endDate,CredManr.PauseMonths);
  end;
*/
  if (nonblank(CredManr.ProlongDate)) then begin
    CredManr.endDate = AddMonth(CredManr.endDate,CredManr.ProlongMonths);
  end;
  
  return;
end;

function string 255 GetDocumentLangCode(record CredManVc CredManr,Integer type)
begin
  string 255 res;
  record CredManTypeVc CMTr;
  
  CMTr.Code = CredManr.Type;
  if (ReadFirstMain(CMTr,1,true)) then begin
    switch (type) begin
      case 1: res = CMTr.CredManLangCode;
      case 2: res = CMTr.ScheduleLangCode;
      case 3: res = CMTr.SuretyLangCode;
      case 4: res = CMTr.VekselisLangCode;
    end;
  end;

  if (blank(res)) then begin
    res = CredManr.LangCode;
  end;

  GetDocumentLangCode = res;
  return;
end;

function Boolean CanPauseAgreement(record CredManVc CredManr,Date td,val months)
begin
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  Integer i,rwcnt;
  record CredManSetBlock CMb;
  Date td3;
  Boolean res;
  


  if (CredManr.PauseMonths>0) then begin
    td3 = AddMonth(td,months);
  end;

  res = true;
  BlockLoad(CMb);
  BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CMb,true,false);
  rwcnt = MatRowCnt(CredManPayPlanr);
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(CredManPayPlanr,i,CredManPayPlanrw);
    if (CredManPayPlanrw.PlanType>=5) then begin
      if (td<=CredManPayPlanrw.TransDate) then begin
        res = false;
        if (nonblank(td3)) then begin
          if (td3>=CredManPayPlanrw.TransDate and td==CredManr.PauseDate) then begin
            res = true;
          end;
        end;
      end else begin
        if (nonblank(CredManr.PauseDate)) then begin
          if (CredManr.PauseDate<=CredManPayPlanrw.TransDate) then begin
            res = false;
          end;
        end;
      end;
      i = 0;
    end;
  end;

  if (td<CredManr.FirstInvDate) then begin
    res = false;
  end;

  if (CredManr.PrincipleDelayMonths>0) then begin
    if (AddDay(AddMonth(CredManr.FirstInvDate,CredManr.PrincipleDelayMonths),-1)>td) then begin
      res = false;
    end;
  end;

  CanPauseAgreement = res;
  return;
end;

global
updating procedure RecreateAgreementSchedule(record CredManVc CredManr)
begin
  record AgreementDocVc ADr,oldADr;
  row AgreementDocVc ADrw;
  record RcVc RepSpec;
  record ExternalFormBlock EFb;
  string 255 fn,langcode;
  Integer i,rwcnt;

  ADr.RecordType = 0;
  ADr.RecordNr = CredManr.SerNr;
  ADr.Type = 2;
  if (ReadFirstKey("RecordNr",ADr,3,true)) then begin
    ArchiveExistingDocument(ADr);
    RecordCopy(oldADr,ADr);
    ADr.SignedByUs = 0;
    ADr.SignedByUsDate = "";
    ADr.SignedByUsTime = "";
    rwcnt = MatRowCnt(ADr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(ADr,i,ADrw);
      ADrw.Status = 0;
      ADrw.SignatureDate = "";
      ADrw.SignatureTime = "";
      MatRowPut(ADr,i,ADrw);
    end;
    if (RecordUpdate(oldADr,ADr,true)==0) then begin
      //should move this out to a function
      BlockLoad(EFb);
      fn = "Schedule.pdf";
      langcode = GetDocumentLangCode(CredManr,2);
      if (EFb.ExtScheduleForm==1) then begin
        RepSpec.long1 = CredManr.InstalmentType;
        if (CredManr.MaxMonthlyPayment>0) then begin
          RepSpec.flags[10] = 1;
        end;
        PrintEBSDocument("CredManScheduleForm",fn,RepSpec,CredManr.SerNr,langcode);
      end else begin
        PrintMultiDocDocument("CredManScheduleForm",fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
      end;
      RecordLinkFile(fn,0,ADr,CurrentCompany);
      Delete_File(fn);
    end;
  end;

  return;
end;

global
procedure DoPauseCredMan(var record CredManVc CredManr,val months,val prolongmonths,Date td,var Boolean res)
begin
  res = false;
//  if (nonblank(td)) then begin
    //RecordCopy(oldCredManr,CredManr);
    if (CanPauseAgreement(CredManr,td,months)) then begin
      CredManr.PauseDate = td;
      CredManr.PauseMonths = months;
      if (prolongmonths>=0) then begin
        CredManr.ProlongDate = td;
        CredManr.ProlongMonths = prolongmonths;
      end;
      CredManSetDatesCust(CredManr);
      res = true;      
    end;
    //RecordUpdate(oldCredManr,CredManr,true);
//  end;

  return;
end;

global
function Date GetPauseCredManNextDate(record CredManVc CredManr)
begin
  record CredManVc oldCredManr;
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  Integer i,rwcnt;
  Date res;
  record CredManSetBlock CMb;
  
  if (nonblank(CredManr.PauseDate)) then begin
    res = CredManr.PauseDate;
  end else begin
    BlockLoad(CMb);
    BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CMb,true,false);
    rwcnt = MatRowCnt(CredManPayPlanr);
    res = CredManr.FirstInvDate;
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(CredManPayPlanr,i,CredManPayPlanrw);
      if (CredManPayPlanrw.PlanType<5) then begin
        i = rwcnt;
        res = CredManPayPlanrw.TransDate;
      end;
    end;
  end;

  if (res<CredManr.FirstInvDate) then begin
    res = CredManr.FirstInvDate;
  end;

  if (CredManr.PrincipleDelayMonths>0) then begin
    if (AddDay(AddMonth(CredManr.FirstInvDate,CredManr.PrincipleDelayMonths),-1)>res) then begin
      res = AddMonth(CredManr.FirstInvDate,CredManr.PrincipleDelayMonths);
    end;
  end;
  
  GetPauseCredManNextDate = res;
  return;
end;

global
procedure GetRefinCredManData(Longint CredManNr,Integer type,var record RcVc RepSpec,Boolean initf)
begin
  record CredManVc CredManr;
  record CredManPayPlanVc CredManPayPlanr;
  record CredManTypeVc CredManTyper;
  record RcVc planRepSpec;

  CredManr.SerNr = CredManNr;
  if (ReadFirstMain(CredManr,1,true)) then begin

    if (blank(RepSpec.d1)) then begin
      RepSpec.d1 = CurrentDate;
    end; 

    planRepSpec.long1 = CredManr.SerNr;
    planRepSpec.d1 = RepSpec.d1;
    CheckBuyOutAgreement(CredManr,planRepSpec);
    RepSpec.vals0 = planRepSpec.vals0;
    RepSpec.vals1 = planRepSpec.vals1;
    //RepSpec.vals2 = 0;//should stay blank;
    RepSpec.vals3 = RepSpec.vals0 + RepSpec.vals1 + RepSpec.vals2;

    if (initf) then begin
      RepSpec.long1 = CredManr.SerNr;
      RepSpec.f1 = CredManr.Type;
      CredManTyper.Code = CredManr.Type;
      if (ReadFirstMain(CredManTyper,1,true)) then begin
        RepSpec.flags[0] = CredManTyper.InstalmentMonths;
      end;
      if (type==2) then begin
        RepSpec.f2 = CredManr.SuretyProvider1;
      end else begin
        RepSpec.f2 = CredManr.CustCode;
      end;

      RepSpec.flags[1] = CredManr.MonthlyPaymentDay;
      RepSpec.vals4 = 0;
      if (type==2) then begin
        RepSpec.vals4 = CredManr.SuretyInterestRate;
      end;

      RepSpec.ArtMode = type;
    end;
  end;

  return;
end;

procedure CopySingleCredManSurety(var record CredManVc CredManr,record CredManVc tCredManr,Integer num)
begin
  
    SetFieldValueByName(CredManr,"SuretyProvider" & num,GetFieldValueByName(tCredManr,"SuretyProvider" & num,-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Name",GetFieldValueByName(tCredManr,"Surety" & num & "Name",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Position",GetFieldValueByName(tCredManr,"Surety" & num & "Position",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Phone",GetFieldValueByName(tCredManr,"Surety" & num & "Phone",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Mobile",GetFieldValueByName(tCredManr,"Surety" & num & "Mobile",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "eMail",GetFieldValueByName(tCredManr,"Surety" & num & "eMail",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "ID",GetFieldValueByName(tCredManr,"Surety" & num & "ID",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr0",GetFieldValueByName(tCredManr,"Surety" & num & "Addr0",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr1",GetFieldValueByName(tCredManr,"Surety" & num & "Addr1",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr2",GetFieldValueByName(tCredManr,"Surety" & num & "Addr2",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr3",GetFieldValueByName(tCredManr,"Surety" & num & "Addr3",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr4",GetFieldValueByName(tCredManr,"Surety" & num & "Addr4",-1),-1);

  return;
end;

procedure CopyCredManSurety(var record CredManVc CredManr,record CredManVc tCredManr)
begin
  
  CopySingleCredManSurety(CredManr,tCredManr,1);
  CopySingleCredManSurety(CredManr,tCredManr,2);
  CopySingleCredManSurety(CredManr,tCredManr,3);

  return;
end;

updating procedure CreateRefinCredMan(record RcVc RepSpec,var record CredManVc CredManr,record CredManVc tCredManr)
begin
  
  RecordNew(CredManr);
  CredManr.startDate = RepSpec.d1;
  CredManr.FirstInvDate = RepSpec.d1;
  CredManr.InvSum4 = RepSpec.vals3;
  CredManr.Type = RepSpec.f1;
  CredManr.CustCode = RepSpec.f2;
  PasteCustInCredMan(CredManr);
  CredManVc_PasteType(CredManr);
  CredManr.IntRate = RepSpec.vals4;
  CredManr.ClientContact = tCredManr.ClientContact;
  CredManr.MonthlyPaymentDay = RepSpec.flags[1];
  CredManr.InstalmentMonths = RepSpec.flags[0];
  CredManSetDatesCust(CredManr); 
  CopyCredManSurety(CredManr,tCredManr);
  CredManr.LoanApplication = tCredManr.LoanApplication;
  CredManr.SerNr = NextSerNr("CredManVc",CredManr.startDate,-1,false,"");
  CredManr.OKFlag = 1;
  CredManr.Approved = 1;
  RecordInsert(CredManr,true);

  return;
end;

updating procedure CreditAndOKInvoice(Longint ivnr)
begin
  record IVVc IVr,IVCreditr;

  IVr.SerNr = ivnr;
  if (ReadFirstMain(IVr,1,true)) then begin
    if (CreateCreditNoteIV(IVr,kInvoiceTypeCredit,IVCreditr,"",false)==0) then begin
      IVCreditr.SerNr = NextSerNr("IVVc",IVCreditr.InvDate,-1,false,"");
      IVCreditr.OKFlag = 1;
      RecordInsert(IVCreditr,true);
    end;
  end;

  return;
end;

updating procedure CreditCredManInvoices(record CredManVc CredManr)
begin
  Boolean TrHs;
  record CredHistVc CredHistr;
  vector Boolean vInv;
  array string 255 invarr;
  record ARVc ARr;
  Integer i;

  TrHs = true;
  CredHistr.CredManNr = CredManr.SerNr;
  while (LoopKey("CredManChargeType",CredHistr,1,TrHs)) begin
    if (CredHistr.CredManNr!=CredManr.SerNr) then begin
      TrHs = false;
    end else begin
      if (CredHistr.RecordType==0) then begin
        vInv[CredHistr.RecordNr] = true;
      end;
    end;
  end;

  GetVectorTags(vInv,invarr);
  for (i=0;i<invarr.length;i=i+1) begin
    ARr.InvoiceNr = StringToLongint(invarr[i]);
    if (ReadFirstMain(ARr,1,true)) then begin
      if (ARr.RVal>0) then begin
        CreditAndOKInvoice(ARr.InvoiceNr);
      end;
    end;
  end;
  
  return;
end;

global
updating function Boolean DoRefinCredMan(record RcVc RepSpec,var string msg,var record CredManVc nCredManr)
begin
  record CMOrderClassBlock OCb;
  record IVVc IVr;
  record CredManVc CredManr,oldCredManr;
  Boolean res;
  
  CredManr.SerNr = RepSpec.long1;
  if (ReadFirstMain(CredManr,1,true)) then begin
    BlockLoad(OCb);
    RecordCopy(oldCredManr,CredManr);
    CredManr.OrderClass = OCb.RefinClass;
    if (RecordUpdate(oldCredManr,CredManr,true)==0) then begin
      CreateRefinCredMan(RepSpec,nCredManr,CredManr);
      DoBuyOutAgreement(CredManr,IVr);
      CreditCredManInvoices(CredManr);
      res = true;
    end;
  end;

  DoRefinCredMan = res;
  return;
end;

global
procedure GetFactoringRecord(record CredManVc CredManr,var record CredManFactVc CMFr)
begin
  record CredManFactVc tCMFr;

  tCMFr.CredManNr = CredManr.SerNr;
  if (ReadFirstMain(tCMFr,1,true)) then begin
    RecordCopy(CMFr,tCMFr);
  end else begin
    RecordNew(CMFr);
    CMFr.CredManNr = CredManr.SerNr;
  end;

  return;
end;

global
procedure GetPayGrntRecord(record CredManVc CredManr,var record CredManPmtGrntVc PGr)
begin
  record CredManPmtGrntVc tPGr;

  tPGr.CredManNr = CredManr.SerNr;
  if (ReadFirstMain(tPGr,1,true)) then begin
    RecordCopy(PGr,tPGr);
  end else begin
    RecordNew(PGr);
    PGr.CredManNr = CredManr.SerNr;
  end;

  return;
end;

function Integer GetSuretyCnt(record CredManVc CredManr)
begin
  Integer res;
  
  res = 0;
  if (nonblank(CredManr.SuretyProvider1)) then begin
    res = res + 1;
  end;
  if (nonblank(CredManr.SuretyProvider2)) then begin
    res = res + 1;
  end;
  if (nonblank(CredManr.SuretyProvider3)) then begin
    res = res + 1;
  end;

  GetSuretyCnt = res;
  return;
end;

procedure AddCustToDocument(var record AgreementDocVc ADr,string code,string name)
begin
  row AgreementDocVc ADrw;
  
  ClearRow(ADr,ADrw,1);
  ADrw.CustCode = code;
  ADrw.CustName = name;
  MatRowPut(ADr,MatRowCnt(ADr),ADrw);

  return;
end;

function Boolean CustIsCEO(string custcode,record CredManVc CredManr)
begin
  Boolean res,foundf;
  record B2BLoanApplicationVc LAr;
  record RLinkVc RLr;

  if (ReadRecordLink(CredManr,1,LAr,RLr)) then begin
    foundf = true;
  end else begin
    if (CredManr.LoanApplication>0) then begin
      LAr.SerNr = CredManr.LoanApplication;
      if (ReadFirstMain(LAr,1,true)) then begin
        foundf = true;
      end;
    end;
  end;
  if (foundf) then begin
    if (LAr.CEOCustCode==custcode) then begin
      res = true;
    end;
  end;  

  CustIsCEO = res;
  return;
end;


procedure AddSuretyToDocument(var record AgreementDocVc ADr,record CredManVc CredManr)
begin
  Integer i;
  string 255 custcode;
  row AgreementDocVc ADrw;
   
  for (i=1;i<4;i=i+1) begin
    custcode = GetFieldValueByName(CredManr,"SuretyProvider" & i,-1);
    if (nonblank(custcode)) then begin
      if (CustIsCEO(custcode,CredManr)==false) then begin
        AddCustToDocument(ADr,custcode,GetFieldValueByName(CredManr,"Surety" & i & "Name",-1))
      end;
    end;
  end;

  return;
end;

procedure GetDocumentSigners(Integer type,record CredManVc CredManr,var Boolean flag1,var Boolean flag2,var Boolean flag3)
begin
  record CredManTypeVc CMTr;
  
  CMTr.Code = CredManr.Type;
  if (ReadFirstMain(CMTr,1,true)) then begin
    switch (type) begin
      case 1: 
        flag1 = CMTr.CredManCEO1==1; flag2 = CMTr.CredManCEO2==1; flag3 = CMTr.CredManSurety==1;
      case 3: 
        flag1 = CMTr.SuretyCEO1==1; flag2 = CMTr.SuretyCEO2==1; flag3 = CMTr.SuretySurety==1;
      case 2: 
        flag1 = CMTr.ScheduleCEO1==1; flag2 = CMTr.ScheduleCEO2==1; flag3 = CMTr.ScheduleSurety==1;
      case 6: 
        flag1 = CMTr.VekselisCEO1==1; flag2 = CMTr.VekselisCEO2==1; flag3 = CMTr.VekselisSurety==1;
    end;
  end;

  return;
end;

function Boolean CustIsSurety(string custcode,record CredManVc CredManr)
begin
  Boolean res;
  
  if (GetFieldValueByName(CredManr,"SuretyProvider1",-1)==custcode) then begin
    res = true;
    goto LCustIsSurety;
  end;
  if (GetFieldValueByName(CredManr,"SuretyProvider2",-1)==custcode) then begin
    res = true;
    goto LCustIsSurety;
  end;
  if (GetFieldValueByName(CredManr,"SuretyProvider3",-1)==custcode) then begin
    res = true;
    goto LCustIsSurety;
  end;

LCustIsSurety:;
  CustIsSurety = res;
  return;
end;

procedure AddCEOToDocument(record AgreementDocVc ADr,record CredManVc CredManr,Boolean suretyf)
begin
  record B2BLoanApplicationVc LAr;
  record RLinkVc RLr;
  Boolean testf,foundf;

  if (ReadRecordLink(CredManr,1,LAr,RLr)) then begin
    foundf = true;
  end else begin
    if (CredManr.LoanApplication>0) then begin
      LAr.SerNr = CredManr.LoanApplication;
      if (ReadFirstMain(LAr,1,true)) then begin
        foundf = true;
      end;
    end;
  end;
  if (foundf) then begin
    if (nonblank(LAr.CEOCustCode)) then begin
      testf = CustIsSurety(LAr.CEOCustCode,CredManr);
      if (suretyf and testf) then begin
        AddCustToDocument(ADr,LAr.CEOCustCode,LAr.CEOName);
      end;
      if (!suretyf and !testf) then begin
        AddCustToDocument(ADr,LAr.CEOCustCode,LAr.CEOName);
      end;
    end;
  end;

  return;
end;

global
updating procedure CreateAgreementDoc(record CredManVc CredManr,Integer type,var record AgreementDocVc ADr)
begin
  string 255 fn,langcode;
  Integer scnt;
  row AgreementDocVc ADrw;
  area filearea;
  record RcVc RepSpec;
  record ExternalFormBlock EFb;
  Boolean flags1,flags2,flags3;
  
  RecordNew(ADr);
  ADr.SerNr = NextSerNr("AgreementDocVc",CurrentDate,-1,false,"");
  ADr.TransDate = CurrentDate;
  ADr.TransTime = CurrentTime;
  ADr.RecordNr = CredManr.SerNr;
  ADr.Type = type;
  
  GetDocumentSigners(ADr.Type,CredManr,flags1,flags2,flags3);
  if (flags1) then begin
    AddCEOToDocument(ADr,CredManr,false);    
  end;
  if (flags2) then begin
    AddCEOToDocument(ADr,CredManr,true);
  end;
  if (flags3) then begin
    AddSuretyToDocument(ADr,CredManr);    
  end;
  if (RecordInsert(ADr,true)) then begin
    scnt = GetSuretyCnt(CredManr);
    BlockLoad(EFb);
    switch (ADr.Type) begin
      case 1://agreement
        fn = "Agreement.pdf";
        langcode = GetDocumentLangCode(CredManr,1);
        if (EFb.ExtCredManForm==1) then begin
          RepSpec.long1 = CredManr.InstalmentType;
          if (CredManr.MaxMonthlyPayment>0) then begin
            RepSpec.flags[10] = 1;
          end;
          PrintEBSDocument("CredManForm",fn,RepSpec,CredManr.SerNr,langcode);
        end else begin
          PrintMultiDocDocument("CredManLongForm",fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        end;
      case 2://schedule
        fn = "Schedule.pdf";
        langcode = GetDocumentLangCode(CredManr,2);
        if (EFb.ExtScheduleForm==1) then begin
          RepSpec.long1 = CredManr.InstalmentType;
          if (CredManr.MaxMonthlyPayment>0) then begin
            RepSpec.flags[10] = 1;
          end;
          PrintEBSDocument("CredManScheduleForm",fn,RepSpec,CredManr.SerNr,langcode);
        end else begin
          PrintMultiDocDocument("CredManScheduleForm",fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        end;
      case 3://surety
        fn = "Surety.pdf";
        langcode = GetDocumentLangCode(CredManr,3);
        if (EFb.ExtSuretyAgreementForm==1) then begin
          RepSpec.long1 = CredManr.InstalmentType;
          if (CredManr.MaxMonthlyPayment>0) then begin
            RepSpec.flags[10] = 1;
          end;
          RepSpec.flags[11] = scnt;
          PrintEBSDocument("SuretyAgreementForm",fn,RepSpec,CredManr.SerNr,langcode);
        end else begin
          PrintMultiDocDocument("SuretyAgreementForm" & scnt,fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        end;
      case 6://vekselis
        fn = "Vekselis.pdf";
        langcode = GetDocumentLangCode(CredManr,4);
        if (EFb.ExtVekselisForm==1) then begin
          RepSpec.long1 = CredManr.InstalmentType;
          if (CredManr.MaxMonthlyPayment>0) then begin
            RepSpec.flags[10] = 1;
          end;
          PrintEBSDocument("VekselisForm",fn,RepSpec,CredManr.SerNr,langcode);
        end else begin
          PrintMultiDocDocument("VekselisForm" & scnt,fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        end;
    end;
    RecordLinkFile(fn,0,ADr,CurrentCompany);
    Delete_File(fn);
  end;

  return;
end;

global
function Boolean GetAgreementDoc(record CredManVc CredManr,Integer type,var record AgreementDocVc ADr)
begin
  record AgreementDocVc tADr;
  Boolean res;
  
  tADr.RecordNr = CredManr.SerNr;
  tADr.RecordType = 0;
  tADr.Type = type;
  if (ReadFirstKey("RecordNr",tADr,3,true)) then begin
    RecordCopy(ADr,tADr);
    res = true;
  end else begin
    qupdating.CreateAgreementDoc(CredManr,type,ADr);
    res = true;
  end;

  GetAgreementDoc = res;
  return;
end;

global
updating procedure CreateAgreementDoc2(Longint sernr,string custcode,Integer rw,var record AgreementDocVc ADr)
begin
  string 255 fn;
  Integer scnt;
  row AgreementDocVc ADrw;
  record B2BLoanApplicationVc LAr;
  record RLinkVc RLr;
  record RcVc RepSpec;
  
  RecordNew(ADr);
  ADr.SerNr = NextSerNr("AgreementDocVc",CurrentDate,-1,false,"");
  ADr.TransDate = CurrentDate;
  ADr.TransTime = CurrentTime;
  ADr.RecordNr = sernr;
  ADr.Type = 8;
  if (RecordInsert(ADr,true)) then begin
    fn = "PaymentCertificate.pdf";
    RepSpec.ArtMode = rw;
    PrintMultiDocDocument2("PaymentCertificateForm",fn,sernr,0,false,"",RepSpec);
    RecordLinkFile(fn,0,ADr,CurrentCompany);
    Delete_File(fn);
  end;


  return;
end;

global
function Boolean GetAgreementDoc2(Longint sernr,string custcode,Integer rw,var record AgreementDocVc ADr)
begin
  record AgreementDocVc tADr;
  Boolean res,TrHs;
  
  TrHs = true;
  tADr.RecordNr = sernr;
  tADr.RecordType = 0;
  tADr.Type = 8;
  while (LoopKey("RecordNr",tADr,3,TrHs)) begin
    if (tADr.RecordNr!=sernr or tADr.RecordType!=0 or tADr.Type!=8) then begin
      TrHs = false;
    end else begin
      if (ADr.CustCode==custcode) then begin
        RecordCopy(ADr,tADr);
        res = true;
        goto LGetAgreementDoc2;
      end;
    end;
  end;
  qupdating.CreateAgreementDoc2(sernr,custcode,rw,ADr);
  res = true;//no point of having a function i guess

LGetAgreementDoc2:;
  GetAgreementDoc2 = res;
  return;
end;


updating procedure CredManCreditAllInvoices(record CredManVc CredManr)
begin
  record ARVc ARr;
  Boolean TrHs;
  record IVVc IVr,oldIVr,IVCreditr;
  record LocalMachineBlock LMb;
  Integer err1,err2;
  array record IVVc aIVr;
  Integer i;

  ARr.CustCode = CredManr.CustCode;
  TrHs = true;
  while (LoopKey("CustCode",ARr,1,TrHs)) begin
    if (ARr.CustCode!=CredManr.CustCode) then begin
      TrHs = false;
    end else begin
      if (ARr.RVal>0) then begin
        IVr.SerNr = ARr.InvoiceNr;
        if (ReadFirstMain(IVr,1,true)) then begin
          aIVr[aIVr.length] = IVr;
        end;
      end;
    end;
  end;
  for (i=0;i<aIVr.length;i=i+1) begin
    IVr = aIVr[i];
    RecordClear(IVCreditr);
    if (CreateCreditNoteIV(IVr,kInvoiceTypeCredit,IVCreditr,"",false)==0) then begin
      if (IVr.InvDate>CurrentDate) then begin
        IVCreditr.InvDate = IVr.InvDate;   
        IVVc_PasteInvDate(IVCreditr,LMb,err1,err2);
      end;
      IVCreditr.SerNr = NextSerNr("IVVc",IVCreditr.InvDate,-1,false,"");
      if (RecordInsert(IVCreditr,true)) then begin
        RecordCopy(oldIVr,IVCreditr);
        IVCreditr.OKFlag = 1;
        RecordUpdate(oldIVr,IVCreditr,true);
      end;
    end;
  end;
 

  return;
end;

updating procedure CreateCredmanPurchaseInvoice(record CredManVc CredManr,val credsum,var record VIVc VIr)
begin
  record CreditalInvoiceBlock CIb;
  record VIVc oldVIr;
  row VIVc VIrw;
  val ct;
  string 255 warning;

  BlockLoad(CIb);
  RecordNew(VIr);
  VIr.SerNr = NextSerNr("VIVc",CurrentDate,-1,false,"");
  VIr.TransDate = CurrentDate;
  VIr.InvDate = CurrentDate;
  VIVc_PasteInvDate(VIr);    
  VIr.VECode = CredManr.CustCode;
  VIVc_PasteVECode(VIr,0,true,true,warning)
  VIr.PayDeal = CIb.PaymentPayDeal;
  VIVc_PastePayDeal(VIr);
  VIr.Objects =  AddObjectToObjectList(VIr.Objects,CIb.PaymentObject);
  VIr.PayVal = CredManr.InvSum4;
  ClearRow(VIr,VIrw,1);
  VIrw.AccNumber = CIb.PaymentAccount;
  MatRowPut(VIr,0,VIrw);
  VIVc_PasteAccNumber(VIr,"",true,0);
  MatRowGet(VIr,0,VIrw);
  VIrw.VATCode = CIb.PaymentVATCode;
  MatRowPut(VIr,0,VIrw);
  VIDDefault(0,"VATCode",VIr);
  MatRowGet(VIr,0,VIrw);
  VIrw.Sum = CredManr.InvSum4;
  MatRowPut(VIr,0,VIrw);
  VICalcVals(VIr);
  VISumup(VIr,ct);
  if (RecordInsert(VIr,true)) then begin
    RecordCopy(oldVIr,VIr);
    VIr.OKFlag = 1;
    RecordUpdate(oldVIr,VIr,true);
  end; 

  return;
end;

updating procedure CreatePaymentFakeReceipt(record CredManVc CredManr,record IVVc IVr)
begin
  val chk;
  Boolean installmentf;
  record IPVc IPr,oldIPr;
  row IPVc IPrw;
  record CreditalInvoiceBlock CIb;

  BlockLoad(CIb);
  IPr.TransDate = IVr.InvDate;
  IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
  IPr.RegDate = IVr.InvDate;
  IPr.OKFlag = 0;
  IPr.PayMode = CIb.PaymentMode;
  IPPastePayMode(IPr);

  ClearRow(IPr,IPrw,1);
  IPrw.InvoiceNr = IVr.SerNr;
  MatRowPut(IPr,0,IPrw);
  PasteInvIn2IPr(IPr,0,IPr.TransDate,chk,false,installmentf);

  if (RecordStore(IPr,true)) then begin
    RecordCopy(oldIPr,IPr);
    IPr.OKFlag = 1;
    RecordUpdate(oldIPr,IPr,true);
  end;

  return;
end;

updating procedure CreatePaymentOrder(record CredManVc CredManr,record VIVc VIr,val amount)
begin
  record OPVc OPr,oldOPr;
  row OPVc OPrw;
  val chk;
  Boolean installmentf;
  string 255 warning;
  record CreditalInvoiceBlock CIb;

  BlockLoad(CIb);
  RecordNew(OPr);
  OPr.TransDate = VIr.InvDate;	 
  OPr.PayDate = VIr.InvDate;	 
  OPr.SerNr = NextSerNr("OPVc",OPr.TransDate,-1,false,"");		
  OPr.PayMode = CIb.PaymentMode;
  OPPastePayMode(OPr);

  ClearRow(OPr,OPrw,1);
  OPrw.VISerNr = VIr.SerNr;
  MatRowPut(OPr,0,OPrw);
  PasteInvIn2OPr(OPr,0,OPr.TransDate,1,chk,warning,false,installmentf);
  MatRowGet(OPr,0,OPrw);
  OPrw.BankVal = amount;
  OPrw.RecVal = amount;
  MatRowPut(OPr,0,OPrw);
  OPVc_PasteRecVal(OPr,0);

  if (RecordStore(OPr,true)) then begin
    RecordCopy(oldOPr,OPr);
    OPr.DoneFlag = 1;
    OPr.OrderedFlag = 1;
    RecordUpdate(oldOPr,OPr,true);
  end;

  return;
end;

global
updating procedure CredManMakePaymentRemote(var record CredManVc CredManr,var record VIVc VIr)
begin
  record IVVc IVr;
  record CMOrderClassBlock OCb;

  if (CredManr.InstalmentType!=4 and CredManr.InstalmentType!=5) then begin
    CreateSingleAgreementInvoice(CredManr,CredManr.startDate,IVr);
    CheckAndCreateAutoMailFromInvoice(IVr);
    
    //CredManCreditAllInvoices(CredManr);
    CreatePaymentFakeReceipt(CredManr,IVr);
    CreateCredmanPurchaseInvoice(CredManr,IVr.Sum4,VIr);
    CreatePaymentOrder(CredManr,VIr,IVr.Sum4);
    if (VIr.SerNr>0) then begin
      BlockLoad(OCb);
      CredManr.VINr = VIr.SerNr;
      CredManr.OrderClass = OCb.ActiveCredOrderClass;
    end;
  end;


  return;
end;

function string 255 GetDefaultPayMode()
begin
  record PMBlock PMb;
  row PMBlock PMbrw;
  string 255 res;

  BlockLoad(PMb);
  MatRowGet(PMb,0,PMbrw);
  res = PMbrw.Code;

  GetDefaultPayMode = res;
  return;
end;

global
updating procedure CredManPartialBuyoutDo(Date td,val buyoutval,Longint sernr)
begin
  record CredManVc CredManr,oldCredManr;
  record SMVc SMr;
  row SMVc SMrw;
  val tval;
  Longint histnr;
  record BaseCurBlock BCb;

  CredManr.SerNr = sernr;
  if (ReadFirstMain(CredManr,1,true)) then begin
    RecordCopy(oldCredManr,CredManr);
    if (CredManr.VersionNo<0) then begin
      CredManr.VersionNo = 0;
    end;
    CredManr.VersionNo = CredManr.VersionNo + 1;
    RecordUpdate(oldCredManr,CredManr,true);

    BlockLoad(BCb);
    SMr.RegDate = td;
    SMr.Sign = GetDefaultPayMode;
    ClearRow(SMr,SMrw,1);
    SMrw.Objects = CredManr.CustCode;
    SMrw.DebVal = buyoutval;
    SMrw.CurncyCode = BCb.BaseCur1;
    MatRowPut(SMr,0,SMrw);

    ReceiptEntryCreateReceipts(SMr,false,tval);
    if (tval>0) then begin
      CreateCredHist(-1,11,CredManr.CustCode,tval,4,"",td,CredManr.SerNr,td,histnr,"","",1,CredManr.SerNr,tval,-1,tval);
      UpdateCredHistBalancePartialBuyout(CredManr.SerNr,histnr);
    end;
  end;
  
  return;
end;

global
function Boolean ContractHasInvoiceCreated(record CredManVc CredManr)
begin
  record CredHistVc CredHistr;
  Boolean res;
  record CredManSetBlock CMSb;
  
  BLockLoad(CMSb);

  CredHistr.CredManNr = CredManr.SerNr;
  CredHistr.ArtCode = CMSb.RateItem;
  if (ReadFirstKey("ArtCode",CredHistr,2,true)) then begin
    res = true;
  end;

  ContractHasInvoiceCreated = res;
  return;
end;