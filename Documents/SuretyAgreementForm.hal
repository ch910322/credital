external inner function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external inner function Integer NumberofDocumentPages(string,Integer);
external inner procedure CommonDocumentFields(record RcVc);
external inner procedure OutAddress(string,string,string,string,string,string,string);
external inner procedure PrintCurrencyCode(string,string);
external inner procedure ValToText(val,Integer,string,string,var string);
external inner function string 255 CredManOwnBankAccount(Longint);

global
procedure PrintDocumentSigner()
begin
  record CMApplicationSetBlock ASb;
  record UserVc Userr;
  
  BlockLoad(ASb);
  Userr.Code = ASb.DocumentSigner;
  if (ReadFIrstMain(Userr,1,true)) then begin
    OUTFORMFIELD("F_DOCUMENTSIGNERNAME",Userr.Name);
    OUTFORMFIELD("F_DOCUMENTSIGNERPOSITION",ASb.DocumentSignerPosition);
    OUTFORMFIELD("F_DOCUMENTSIGNERCODE",Userr.Code);
  end;

  return;
end;

procedure GetSuretyValues(record CredManVc CredManr,Integer num,var string name,var string phone,var string mobile,var string email,var string id,var string addr0,var string addr1,var string addr2,var string addr3,var string addr4)
begin
  record CUVc CUr;
  
  name = GetFieldValueByName(CredManr,"Surety" & num & "Name",-1);
  email = GetFieldValueByName(CredManr,"Surety" & num & "eMail",-1);
  phone = GetFieldValueByName(CredManr,"Surety" & num & "Phone",-1);
  mobile = GetFieldValueByName(CredManr,"Surety" & num & "Mobile",-1);
  id = GetFieldValueByName(CredManr,"Surety" & num & "ID",-1);
  CUr.Code = GetFieldValueByName(CredManr,"SuretyProvider" & num,-1);
  if (ReadFirstMain(CUr,1,true)) then begin
    addr0 = GetFieldValueByName(CredManr,"Surety" & num & "Addr0",-1);
    addr1 = GetFieldValueByName(CredManr,"Surety" & num & "Addr1",-1);
    addr2 = GetFieldValueByName(CredManr,"Surety" & num & "Addr2",-1);
    addr3 = GetFieldValueByName(CredManr,"Surety" & num & "Addr3",-1);
    addr4 = GetFieldValueByName(CredManr,"Surety" & num & "Addr4",-1);
  end else begin
    addr0 = ""; addr1 = ""; addr2 = ""; addr3 = ""; addr4 = ""; 
  end;

  return;
end;

global
function Boolean FindCEOContact(string custcode,var record CUVc CUr)
begin
  record CMApplicationSetBlock ASb;
  record ContactRelVc CRr;
  Boolean res;
  
  BlockLoad(ASb);
  if (nonblank(ASb.CEOTitle)) then begin
    while (LoopKey("CustContactCode:" & custcode,CRr,1,true)) begin
      CUr.Code = CRr.ContactCode;
      if (ReadFirstMain(CUr,1,true)) then begin
        if (SetInSet(ASb.CEOTitle,CUr.JobDesc)) then begin
          res = true;
          goto LFindCEOContact;
        end;
      end;
    end;
  end;  

LFindCEOContact:;
  FindCEOContact = res;
  return;
end;

global
procedure DoSuretyAgreementForm(record RcVc RepSpec,record CredManVc CredManr)
BEGIN
  record DocVc Docr;
  record CUVc CUr;
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Integer i,rwcnt;
  string 255 tstr;
  Boolean printf,langf;
  Integer intdocnr;
  string 30 formcode,langcode;
  record UserVc Userr;
  string 255 name,phone,email,id,addr0,addr1,addr2,addr3,addr4,mobile;
  Integer surety;
    
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoSuretyAgreementForm;
  end;
  rwcnt = MatRowCnt(FDr);
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(FDr,i,FDrw);
    if (FDrw.LangCode==langcode) then begin
      i = -1;
      langf = true;
    end;
  end;
  if (langf==false) then begin langcode = ""; end;

  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    if (nonblank(RepSpec.f12)) then begin
      formcode = RepSpec.f12;
      printf = false;
    end else begin
      for (i=rwcnt-1;i>=0;i=i-1) begin
        MatRowGet(FDr,i,FDrw);
        if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,CredManr.SerNr,FDrw.PrintGroupCode,langcode,intdocnr,"",formcode)) then
        begin
          goto LBREAK;
        end;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin  
      if (OpenForm(formcode)) then begin
        CommonDocumentFields(RepSpec);
        NumberofDocumentPages(formcode,MatRowCnt(CredManr));
        Docr.Code = formcode;
        if (ReadFirstMain(Docr,1,true)) then begin
        end;
//rest of fields go here

        GetSuretyValues(CredManr,1,name,phone,mobile,email,id,addr0,addr1,addr2,addr3,addr4);
        OUTFORMFIELD("F_CONTACTNAME",name)
        OUTFORMFIELD("F_CONTACTPHONE",phone)
        OUTFORMFIELD("F_CONTACTMOBILE",mobile)
        OUTFORMFIELD("F_CONTACTMAIL",email)
        OUTFORMFIELD("F_CONTACTID",id)
        OutAddress("F_CONTACTADRESS",name,addr0,addr1,addr2,addr3,addr4);

        GetSuretyValues(CredManr,2,name,phone,mobile,email,id,addr0,addr1,addr2,addr3,addr4);
        OUTFORMFIELD("F_CONTACT2NAME",name)
        OUTFORMFIELD("F_CONTACT2PHONE",phone)
        OUTFORMFIELD("F_CONTACT2MOBILE",mobile)
        OUTFORMFIELD("F_CONTACT2MAIL",email)
        OUTFORMFIELD("F_CONTACT2ID",id)
        OutAddress("F_CONTACT2ADRESS",name,addr0,addr1,addr2,addr3,addr4);

        GetSuretyValues(CredManr,3,name,phone,mobile,email,id,addr0,addr1,addr2,addr3,addr4);
        OUTFORMFIELD("F_CONTACT3NAME",name)
        OUTFORMFIELD("F_CONTACT3PHONE",phone)
        OUTFORMFIELD("F_CONTACT3MOBILE",mobile)
        OUTFORMFIELD("F_CONTACT3MAIL",email)
        OUTFORMFIELD("F_CONTACT3ID",id)
        OutAddress("F_CONTACT3ADRESS",name,addr0,addr1,addr2,addr3,addr4);

        CUr.Code = CredManr.CustCode;
        ReadFirstMain(CUr,1,true);
        OUTFORMFIELD("F_NUMMER",CredManr.SerNr);
        OUTFORMFIELD("F_KUNDNAMN",CUr.Name);
        OutFormField("F_REGNUMBER1",CUr.RegNr1);//Customer Registration No. 1
        OutAddress("F_ADRESS",CUr.Name,CUr.InvAddr0,CUr.InvAddr1,CUr.InvAddr2,CUr.InvAddr3,CUr.InvAddr4);
        PrintCurrencyCode("F_VALUTA",CredManr.CurncyCode);
        OUTFORMFIELD("F_TOTSUM",CredManr.InvSum4);
        if (FIELDINFORM("F_TOTSUMINTEXT")) then begin
          ValToText(CredManr.InvSum4,M4Val,CredManr.CurncyCode,CUr.LangCode,tstr);
          OUTFORMFIELD("F_TOTSUMINTEXT",tstr);
        end;

        RecordCLear(CUr);
        if (FindCEOContact(CredManr.CustCode,CUr)) then begin
          OUTFORMFIELD("F_CEONAME",CUr.Name);
          OUTFORMFIELD("F_CEOID",CUr.RegNr1);
        end;

        PrintDocumentSigner;
        OutFormField("F_CREDHALFSUM",(CredManr.InvSum4/2));
        if (FIELDINFORM("F_CREDHALFSUMTEXT")) then begin
          ValToText((CredManr.InvSum4/2),M4Val,CredManr.CurncyCode,CUr.LangCode,tstr);
          OUTFORMFIELD("F_CREDHALFSUMTEXT",tstr);
        end;
        //CUST-BEGIN
        OUTFORMFIELD("F_CUSTOWNBANKACC",CredManOwnBankAccount(CredManr.SerNr));
        //CUST-END

        CloseForm;        
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoSuretyAgreementForm:;  
  RETURN;  
END;     
 

global
procedure SuretyAgreementForm(record RcVc RepSpec)
BEGIN
  record CredManVc CredManr;
  Boolean tmpf;
  
  CredManr.SerNr = RepSpec.long1;
  if (ReadFirstMain(CredManr,1,true)) then begin
    tmpf = PrintDocument(CredManr,"SuretyAgreementForm",false);    
  end;
  RETURN;
END;