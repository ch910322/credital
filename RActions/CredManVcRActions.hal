external inner procedure CredManVc_PasteType(var record CredManVc);
external inner procedure CUCreditLimit(record CUVc,Date,string,var val,var LongInt);


global
function LongInt CredManVcRecordDefaults(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record BaseCurBlock BCb;
  record CredManSetBlock CMb;

  BlockLoad(BCb);
  BlockLoad(CMb);
  CredManr.CurncyCode = BCb.BaseCur1;
  CredManr.MonthlyPaymentDay = CMb.InvDay;

  res = inner.CredManVcRecordDefaults(CredManr,CredMan2r,stat,long4);
  CredManVc_PasteType(CredManr);
  
  CredManVcRecordDefaults = res;
  return;
END;

global
function LongInt CredManVcRecordDuplicate(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record BaseCurBlock BCb;
  record CredManSetBlock CMb;
  val t;

  BlockLoad(BCb);
  BlockLoad(CMb);
  CredManr.CurncyCode = BCb.BaseCur1;
  CredManr.MonthlyPaymentDay = CMb.InvDay;
/*
  CredManr.FactInvAmount = t;
  CredManr.LoanToValue = t;
  CredManr.FactBuyer = "";
  CredManr.FactBuyerName = "";
  CredManr.FactInvNumber = "";
  CredManr.FactComment = "";
  CredManr.FactInsured = 0;
*/

  res = inner.CredManVcRecordDefaults(CredManr,CredMan2r,stat,long4);
  
  CredManVcRecordDuplicate = res;
  return;
END;

function val GetFactoringTotalAgreementWorth(string cucode,Date enddate,Longint sernr)
begin
  record CredManVc CredManr;
  Boolean TrHs;
  val res;

  TrHs = true;
  CredManr.FactBuyer = cucode;
  CredManr.endDate = CurrentDate;
  while (LoopKey("FactBuyer",CredManr,2,TrHs)) begin
    if (CredManr.FactBuyer!=cucode ) then begin//or CredManr.endDate>enddate?
      TrHs = false;
    end else begin
      if (CredManr.startDate<=CurrentDate and CredManr.InstalmentType==4) then begin
        if (CredManr.SerNr!=sernr) then begin
          res = res + CredManr.FactInvAmount;
        end;
      end;
    end;
  end;
  
  GetFactoringTotalAgreementWorth = res;
  return;
end;

global
function LongInt CredManVcRecordCheck(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record CredManVc tCredManr;
  Boolean TrHs,credf;
  Longint crlimitdays;
  val crlimit,totworth;
  record CUVc CUr;

  res = 0;

  if (CredManr.InstalmentType==4) then begin
    if (CredManr.FactInvAmount<=0) then begin
      RecordCheckError(1058,"",-1,"FactInvAmount");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
    if (blank(CredManr.FactInvNumber)) then begin
      RecordCheckError(1058,"",-1,"FactInvNumber");
      res = -1;
      goto LCredManVcRecordCheck;
    end else begin
      TrHs = true;
      tCredManr.FactInvNumber = CredManr.FactInvNumber;
      while (LoopKey("FactInvNumber",tCredManr,1,TrHs)) begin
        if (tCredManr.FactInvNumber!=CredManr.FactInvNumber) then begin
          TrHs = false;
        end else begin
          if (tCredManr.SerNr!=CredManr.SerNr) then begin
            RecordCheckError(200100,"",-1,"FactInvNumber");
            res = -1;
            goto LCredManVcRecordCheck;
          end;
        end;
      end;
    end;
  end;

  if (CredManr.MonthlyPaymentDay<=0) then begin
    RecordCheckError(1058,"",-1,"MonthlyPaymentDay");
    res = -1;
    goto LCredManVcRecordCheck;
  end;

  res = inner.CredManVcRecordCheck(CredManr,CredMan2r,stat,long4);
  
LCredManVcRecordCheck:;
  CredManVcRecordCheck = res;
  return;
END;

global
function LongInt CredManFactVcRecordCheck(var record CredManFactVc CMFr,record CredManFactVc CMF2r,LongInt stat,LongInt long4)
begin
  Longint res;
  record CredManVc CredManr;

  res = 0;
  
  CredManr.SerNr = CMFr.CredManNr;
  if (ReadFirstMain(CredManr,1,true)) then begin
    if (CMFr.Total>CredManr.InvSum4) then begin
      RecordCheckError(200101,"",-1,"Total");
      res = -1;
      goto LCredManFactVcRecordCheck;
    end;
  end;

LCredManFactVcRecordCheck:;
  CredManFactVcRecordCheck = res;
  return;
end;
