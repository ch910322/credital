remote updating procedure CreateSingleAgreementInvoice(record CredManVc,Date,var record IVVc);
remote procedure PasteCustInAgreementRemote(var record CredManVc);
remote updating procedure TerminateCredMan(var record CredManVc,var record IVVc);
remote updating procedure DoBuyOutAgreement(record CredManVc,var record IVVc);
remote procedure CheckBuyOutAgreement(record CredManVc,var record RcVc);
remote inner procedure SetupCustPS2Rn(string,var record RcVc);
remote procedure CredManSetDatesCust(var record CredManVc);
remote procedure GetCredManEventList(record CredManVc,var record ORVc);
external procedure GetCredManEventList2(Longint,var record ORVc);
remote procedure DoPauseCredMan(var record CredManVc,Longint);
remote procedure PasteSuretyInCredMan(var record CredManVc,string,Integer);
remote procedure GetRefinCredManData(Longint,Integer,var record RcVc,Boolean);
remote updating function Boolean DoRefinCredMan(record RcVc,var string,var record CredManVc);
external inner procedure ContactSClassOnOpen(Integer,string,var Array string,var Integer);
remote updating procedure GetFactoringRecord(record CredManVc,var record CredManFactVc);

function Boolean CredManDClassStdProlongFeeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    if (CredManr.StdProlongFee<0) then begin
      CredManr.StdProlongFee = 0;
    end;
    if (CredManr.StdProlongFee>100) then begin
      CredManr.StdProlongFee = 100;
    end;
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassStdProlongFeeEFAfter = true;
  return;
end;

function Boolean CredManDClassStdProlongPrcEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    if (CredManr.StdProlongPrc<0) then begin
      CredManr.StdProlongPrc = 0;
    end;
    if (CredManr.StdProlongPrc>100) then begin
      CredManr.StdProlongPrc = 100;
    end;
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassStdProlongPrcEFAfter = true;
  return;
end;

function Boolean CredManDClassInstalmentMonthsEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    if (CredManr.InstalmentType==4) then begin
      CredManr.InstalmentMonths = 1;
    end;
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassInstalmentMonthsEFAfter = true;
  return;
end;

function Boolean CredManDClassCustCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    PasteCustInAgreementRemote(CredManr);
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassCustCodeEFAfter = true;
  return;
end;

function Boolean CredManDClassstartDateEFAfter(Integer wn,Boolean changedf)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    CredManSetDatesCust(CredManr);
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassstartDateEFAfter = true;
  return;
end;

function Boolean CredManDClassSuretyProviderEFAfter(Integer wn,Boolean changedf,Integer num)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    PasteSuretyInCredMan(CredManr,GetFieldValueByName(CredManr,"SuretyProvider" & num,-1),num);
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassSuretyProviderEFAfter = true;
  return;
end;

procedure CalculateFactoringValue(var record CredManVc CredManr)
begin
  
  CredManr.InvSum4 = CredManr.FactInvAmount*(CredManr.LoanToValue/100);

  return;
end;

function Boolean CredManDClassLoanToValueEFAfter(Integer wn,Boolean changedf,Integer num)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    if (CredManr.LoanToValue<0) then begin
      CredManr.LoanToValue = 0;
    end;
    if (CredManr.LoanToValue>100) then begin
      CredManr.LoanToValue = 100;
    end;
    CalculateFactoringValue(CredManr);
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassLoanToValueEFAfter = true;
  return;
end;

function Boolean CredManDClassFactBuyerEFAfter(Integer wn,Boolean changedf,Integer num)
begin
  record CredManVc CredManr;
  record CUVc CUr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    CUr.Code = CredManr.FactBuyer;
    if (ReadFirstMain(CUr,1,true)) then begin
      CredManr.FactBuyerName = CUr.Name;
    end;
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassFactBuyerEFAfter = true;
  return;
end;

function Boolean CredManDClassFactInvAmountEFAfter(Integer wn,Boolean changedf,Integer num)
begin
  record CredManVc CredManr;

  if (changedf) then begin  
    GetWindowRecord(wn,CredManr);
    if (CredManr.FactInvAmount>0) then begin
      CalculateFactoringValue(CredManr);
    end;
    PutWindowRecord(wn,CredManr);   
  end; 
  CredManDClassFactInvAmountEFAfter = true;
  return;
end;


global
function Boolean CredManDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;

  if (fieldname=="InstalmentMonths" or fieldname=="InstalmentType") then begin//call standard function as well
    res = CredManDClassInstalmentMonthsEFAfter(wn,rownr,changed!=0);
  end;

  switch (fieldname) begin
    case "StdProlongFee":     res = CredManDClassStdProlongFeeEFAfter(wn,rownr,changed!=0);
    case "StdProlongPrc":     res = CredManDClassStdProlongPrcEFAfter(wn,rownr,changed!=0);
    case "startDate":         res = CredManDClassstartDateEFAfter(wn,changed!=0);
    case "MonthlyPaymentDay": res = CredManDClassstartDateEFAfter(wn,changed!=0);
    case "SuretyProvider1":   res = CredManDClassSuretyProviderEFAfter(wn,changed!=0,1);
    case "SuretyProvider2":   res = CredManDClassSuretyProviderEFAfter(wn,changed!=0,2);
    case "SuretyProvider3":   res = CredManDClassSuretyProviderEFAfter(wn,changed!=0,3);
    case "LoanToValue":       res = CredManDClassLoanToValueEFAfter(wn,changed!=0,3);
    case "FactBuyer":         res = CredManDClassFactBuyerEFAfter(wn,changed!=0,3);
    case "FactInvAmount":     res = CredManDClassFactInvAmountEFAfter(wn,changed!=0,3);
    otherwise 
      res = inner.CredManDClassAfterEditField(wn,fieldname,fn,rownr,changed);
  end;
  if (fieldname=="CustCode") then begin//call standard function as well
    res = CredManDClassCustCodeEFAfter(wn,rownr,changed!=0);
  end;
  
  CredManDClassAfterEditField = res;
  return;
end;

global
procedure UpdateCredManStringList(Integer wn,record CredManVc CredManr)
begin
  string 255 tag;
  Integer i,rwcnt;
  record ORVc ORr;
  row ORVc ORrw;
  
/*
  threadremote.GetCredManEventList(CredManr,ORr);

  ClearStringList(wn);
  rwcnt = MatRowCnt(ORr);
  for (i = 0; i<rwcnt; i = i + 1) begin
    MatRowGet(ORr,i,ORrw);
    tag = ORrw.RowOrderType & ":" & ORrw.GroupOrdRow;
    AddListRow(wn,"credmanevents",1,tag,0);
    AddListData(wn,"credmanevents","date",ORrw.PlanShipRowDate);
    AddListData(wn,"credmanevents","duedate",ORrw.PickingDate);
    if (ORrw.RowOrderType==0) then begin
      AddListData(wn,"credmanevents","comment",ORrw.InloadingDate & ":" & ORrw.DespatchRowDate);
    end;
    AddListData(wn,"credmanevents","paid",ORrw.Sum-ORrw.TAX1Reb);
    AddListData(wn,"credmanevents","balance",ORrw.rowGP);
    AddListData(wn,"credmanevents","issued",ORrw.Sum);
  end;
*/

  return;
end;

global
updating procedure CreateCredManInvoiceDsm()
begin
  record CredManVc CredManr;
  record IVVc IVr;
  Integer wn;

  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  CreateSingleAgreementInvoice(CredManr,CurrentDate,IVr);
  if (IVr.SerNr>0) then begin
    OpenWindow("IVDClass",1,0,"","",IVr);
  end;
  //UpdateCredManStringList(wn,CredManr);
  RerunWindowDef(wn);

  return;
end;

global
updating procedure TerminateCredManDsm()
begin
  Integer wn;
  record CredManVc CredManr;
  record IVVc IVr;

  wn = CurWindow;
  if (WindowState(wn)==Rs_Normal) then begin
    GetWindowRecord(wn,CredManr);
    TerminateCredMan(CredManr,IVr);
    PutWindowRecord(wn,CredManr);
    SetWindowState(wn,Rs_Normal);
    if (IVr.SerNr>0) then begin
      OpenWindow("IVDClass",1,0,"","",IVr);
    end;
  end;

  return;
end;

global
updating procedure BuyOutCredManDsm()
begin
  Integer wn;
  record CredManVc CredManr;
  record IVVc IVr;

  wn = CurWindow;
  if (WindowState(wn)==Rs_Normal) then begin
    GetWindowRecord(wn,CredManr);
    DoBuyOutAgreement(CredManr,IVr);
    if (IVr.SerNr>0) then begin
      OpenWindow("IVDClass",1,0,"","",IVr);
    end;
  end;

  return;
end;

global
procedure CalculateBuyOutValue()
begin
  Integer wn;
  record RcVc RepSpec;
  record CredManVc CredManr;
  
  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,RepSpec);
  CredManr.SerNr = RepSpec.long1;
  if (ReadFirstMain(CredManr,1,true)) then begin
    CheckBuyOutAgreement(CredManr,RepSpec);
    PutWindowRecord(wn,RepSpec);
  end;
  SelectWindow(wn);

  return;
end;

global
function Boolean CheckBuyoutValueWClassOnOpenWindow(Integer wn)
begin
  record RcVc RepSpec;

  GetWindowRecord(wn,RepSpec);
  RepSpec.d1 = CurrentDate;
  PutWindowRecord(wn,RepSpec);
  CalculateBuyOutValue;

  CheckBuyoutValueWClassOnOpenWindow = false;
  return;
end;

global
procedure CheckBuyoutValueWindowDsm()
begin
  record RcVc RepSpec;
  Integer wn;
  record CredManVc CredManr;
  
  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  RepSpec.long1 = CredManr.SerNr;
  OpenWindow("CheckBuyoutValueWClass",0,CurWindow,"","",RepSpec);

  return;
end;

global
procedure CUStatusCredManDsm()
begin
  record RcVc RepSpec;
  record CredManVc CredManr;
  Integer wn;

  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  SetupCustPS2Rn(CredManr.CustCode,RepSpec);
  RepSpec.Media = mtScreen;
  RunReport(RepSpec,0);
  return;
end;

global
updating procedure PaymentScheduleCredManDsm()
begin
  Integer wn;
  record CredManVc CredManr;

  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  if (WindowState(wn)==Rs_Normal) then begin
    PrintDocument(CredManr,"CredManScheduleForm",false);//ActDocForm
  end;

  return;
end;

global
procedure PauseCredManDsm()
begin
  Integer wn;
  record RcVc RepSpec;
  record CredManVc CredManr;

  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  if (WindowState(wn)==Rs_Normal) then begin
    
    RepSpec.long1 = CredManr.PauseMonths;
    OpenWindow("PauseCredManWClass",0,wn,"","",RepSpec);
  end;

  return;
end;

global
updating procedure PauseCredManWClassPause()
begin
  Integer wn,mwn;
  record CredManVc CredManr;
  record RcVc RepSpec;

  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,RepSpec);
  if (RepSpec.long1>0) then begin
    mwn = MotherWindow(wn);
    if (WindowState(mwn)==Rs_Normal) then begin
      GetWindowRecord(mwn,CredManr);
      DoPauseCredMan(CredManr,RepSpec.long1);
      PutWindowRecord(mwn,CredManr);
      WindowDoOK(mwn,0);
      //SetWindowState(mwn,Rs_Normal);
    end;
    CloseWindow(wn);
  end;
  return;
end;

procedure UpdateCredManSoftFields(Integer wn,record CredManVc CredManr)
begin
  record RcVc RepSpec;

  RepSpec.d1 = CurrentDate;
  CheckBuyOutAgreement(CredManr,RepSpec);
  PutWindowString(wn,"buyoutvalue",RepSpec.vals2);
  PutWindowString(wn,"remainingprinciple",RepSpec.vals3);
  PutWindowString(wn,"totaldue",RepSpec.vals4);

  return;
end;

global
function Boolean CredManDClassOnOpenWindow(Integer wn)
begin
  Boolean res;
  record CredManVc CredManr;

  GetWindowRecord(wn,CredManr);
  //res = inner.CredManDClassOnOpenWindow(wn);
  //SetWindowSubset(wn,CredManr.SerNr);
  //UpdateCredManStringList(wn,CredManr);
  UpdateCredManSoftFields(wn,CredManr);

  CredManDClassOnOpenWindow = false;
  return;
end;

global
procedure CredManDClassOnWindowRecordChange(Integer wn)
begin
  record CredManVc CredManr;

  GetWindowRecord(wn,CredManr);
  //SetWindowSubset(wn,CredManr.SerNr);
  //logtext(0,"set subset" & CredManr.SerNr);
  //UpdateCredManStringList(wn,CredManr);
  UpdateCredManSoftFields(wn,CredManr);

  return;
end;

global
function Boolean CredManDClassUpdateWindowSubset(Integer wn,var string subsetname)
begin
  record CredManVc CredManr;
  
  GetWindowRecord(wn,CredManr);
  if (CredManr.SerNr>0) then begin
    subsetname = CredManr.SerNr;
  end else begin
    subsetname = -9999999;
  end;

  CredManDClassUpdateWindowSubset = true;
  return;
end;

global
function Boolean CredManDClassListDblClick(Integer wn)
begin
  Integer line;
  Longint pos,sernr;
  record IVVc IVr;
  record IPVc IPr;
  string 255 tstr,tstr2,type;
  record CredHistVc CredHistr;

  ReadMarkedRecord(wn,CredHistr);
  switch (CredHistr.RecordType) begin
    case "0":
      IVr.SerNr = CredHistr.RecordNr;
      if (ReadFirstMain(IVr,1,true)) then begin
        OpenWindow("IVDClass",1,0,"","",IVr);
      end;
    case "1":
      IPr.SerNr = CredHistr.RecordNr;
  end;

  return;
end;

//this allows to edit a field value in hal and use PutWindowRecord and not get "The record has changed" message
//it was needed for TerminateCredManDsm. I combined it with "SetWindowState"
global
function LongInt CredManVcRecordProtectFields(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  CredManr.OrderClass = CredMan2r.OrderClass;
  CredManr.PauseDate = CredMan2r.PauseDate;
  CredManr.PauseMonths = CredMan2r.PauseMonths;
  //CredManr.endDate = CredMan2r.endDate;
  
  CredManVcRecordProtectFields = res;
  return;
end;


global
procedure CredManInfoBuyOutDbl(string dblstr,string l,Integer currepwn)
begin
  Integer wn;
  record RCVc RepSpec;

  RepSpec.long1 = StringToLongint(l);
  OpenWindow("CheckBuyoutValueWClass",0,0,"","",RepSpec);

  return;
end;

function Boolean RefinCredMan2WClassd1EFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record RcVc RepSpec;

  if (changedf) then begin  
    GetWindowRecord(wn,RepSpec);
    GetRefinCredManData(RepSpec.long1,RepSpec.ArtMode,RepSpec,true);
    PutWindowRecord(wn,RepSpec);   
  end; 
  RefinCredMan2WClassd1EFAfter = true;
  return;
end;

function Boolean RefinCredMan2WClassvals2EFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record RcVc RepSpec;

  if (changedf) then begin  
    GetWindowRecord(wn,RepSpec);
    RepSpec.vals3 = RepSpec.vals0 + RepSpec.vals1 + RepSpec.vals2;
    PutWindowRecord(wn,RepSpec);   
  end; 
  RefinCredMan2WClassvals2EFAfter = true;
  return;
end;

function Boolean RefinCredMan2WClassvals3EFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record RcVc RepSpec;

  if (changedf) then begin  
    GetWindowRecord(wn,RepSpec);
    if (RepSpec.vals3<(RepSpec.vals0 + RepSpec.vals1)) then begin
      RepSpec.vals3 = StringToVal(WindEFStr(wn),M4Val);
    end else begin
      RepSpec.vals2 = RepSpec.vals3 - (RepSpec.vals0 + RepSpec.vals1);
    end;
    PutWindowRecord(wn,RepSpec);
  end; 
  RefinCredMan2WClassvals3EFAfter = true;
  return;
end;

function Boolean RefinCredMan2WClassflags1EFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record RcVc RepSpec;

  if (changedf) then begin  
    GetWindowRecord(wn,RepSpec);
    if (RepSpec.flags[1]>31) then begin
      RepSpec.flags[1] = 31;
    end;
    if (RepSpec.flags[1]<1) then begin
      RepSpec.flags[1] = 1;
    end;
    PutWindowRecord(wn,RepSpec);
  end; 
  RefinCredMan2WClassflags1EFAfter = true;
  return;
end;

global
function Boolean RefinCredMan2WClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;

  switch (fieldname) begin
    case "d1":
      res = RefinCredMan2WClassd1EFAfter(wn,rownr,changed!=0);
    case "vals2":
      res = RefinCredMan2WClassvals2EFAfter(wn,rownr,changed!=0);
    case "vals3":
      res = RefinCredMan2WClassvals3EFAfter(wn,rownr,changed!=0);
    case "flags[1]":
      res = RefinCredMan2WClassflags1EFAfter(wn,rownr,changed!=0);
  end;

  RefinCredMan2WClassAfterEditField = res;
  return;
end;

procedure RefinCredMan(Integer type)
begin
  Integer wn;
  record RcVc RepSpec,RepSpec2;

  wn = CurWindow;
  GetWindowRecord(wn,RepSpec);
  
  GetRefinCredManData(RepSpec.long1,type,RepSpec2,true);

  CloseWindow(wn);
  OpenWindow("RefinCredMan2WClass",0,0,"","",RepSpec2);

  return;
end;

global
procedure RefinCredManWClassRefinance()
begin
  RefinCredMan(0);
  return;
end;

global
procedure RefinCredManWClassProlong()
begin
  RefinCredMan(1);
  return;
end;

global
procedure RefinCredManWClassTransfer()
begin
  RefinCredMan(2);
  return;
end;

global
procedure RefinCredManDsm()
begin
  record RcVc RepSpec;
  record CredManVc CredManr;
  Integer wn;

  wn = CurWindow;
  if (WindowState(wn)==Rs_Normal) then begin
    GetWindowRecord(wn,CredManr);
    RepSpec.long1 = CredManr.SerNr;
    OpenWindow("RefinCredManWClass",0,0,"","",RepSpec);
  end;

  return;
end;

global
updating procedure RefinCredMan2WClassSubmit()
begin
  Integer wn;
  record RcVc RepSpec;
  record CredManVc CredManr;
  string 255 msg;

  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,RepSpec);
  if (DoRefinCredMan(RepSpec,msg,CredManr)) then begin
    CloseWindow(wn);
    OpenWindow("CredManDClass",1,0,"","",CredManr);
  end else begin
    Stopalert(msg);
  end;

  return;
end;

function string 255 GetCredHistDetail(record ORVc ORr,Longint sernr,string fieldname)
begin
  string 255 res;
  Integer i,rwcnt;
  row ORVc ORrw;

  rwcnt = MatRowCnt(ORr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORr,i,ORrw);
    if (ORrw.GroupOrdRow==sernr) then begin
      res = GetFieldValueByName(ORr,fieldname,i);
      i = rwcnt;
    end;
  end;

  GetCredHistDetail = res;
  return;
end;

function string 255 GetCredHistDetail2(record ORVc ORr,Longint sernr)
begin
  string 255 res;
  Integer i,rwcnt;
  row ORVc ORrw;

  rwcnt = MatRowCnt(ORr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORr,i,ORrw);
    if (ORrw.GroupOrdRow==sernr) then begin
      res = (ORrw.Sum-ORrw.TAX1Reb);
      i = rwcnt;
    end;
  end;

  GetCredHistDetail2 = res;
  return;
end;

global
procedure CredManVc_ContactSClass(Integer wn,Integer mwn)
BEGIN
  record CredManVc CredManr;
  Array string 60 acontact;
  Integer i,acnt;
  LongInt pos;
  string 255 namestr,titlestr,custcode;

  GetWindowRecord(mwn,CredManr);
  ContactSClassOnOpen(wn,CredManr.CustCode,acontact,acnt);
  for (i=0;i<acnt;i=i+1) begin
//     SetListString(wn,80,acontact[i],false);
    pos = 0;    
    GetNextSubstring(acontact[i],pos,"#",namestr);
    GetNextSubstring(acontact[i],pos,"#",titlestr);
    GetNextSubstring(acontact[i],pos,"#",custcode);
    AddListRow(wn,"DLPasteContact",1,namestr,0);
    AddListData(wn,"DLPasteContact","DLContactName",namestr);
    AddListData(wn,"DLPasteContact","DLContactTitle",titlestr);    
  end;
  RETURN;
END;


global
function Boolean ContactSClassOnOpenWindow(Integer wn)
begin
  Integer mwn;
  Boolean res;

  mwn = MotherWindow(wn);
  switch (GetWindowFileName(mwn)) begin
    case "CredManVc": CredManVc_ContactSClass(wn,mwn);
    otherwise
      res = inner.ContactSClassOnOpenWindow(wn);
  end;
  ContactSClassOnOpenWindow = false;
  return;
end;

global
updating procedure OpenCredManFactoringBtn()
begin
  Integer wn;
  record CredManVc CredManr;
  record CredManFactVc CMFr;

  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  GetFactoringRecord(CredManr,CMFr);
  OpenWindow("CredManFactDClass",1,wn,"","",CMFr);

  return;
end;

global
updating procedure OpenCredManFactoringInvoicesBtn()
begin
  Integer wn;
  record CredManVc CredManr;
  record RcVc RepSpec;

  wn = CurWindow;
  GetWindowRecord(wn,CredManr);
  OpenWindow("CustCredManFactInvoiceWClass",1,wn,"","",RepSpec);

  return;
end;